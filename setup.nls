;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                           SETUP PROCEDURES                           ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; procédure d'initialisation des variables globales
to setup-globals
  ; variables fixées par les sliders :
  set nb_of_persons number_of_persons
  set nb_of_companies number_of_companies
  set nb_of_pairs_considered number_of_pairs_considered
  set nb_of_locations_possibles number_of_locations_possibles
  set min_salary minimum_salary
  set max_salary maximum_salary
  set minimum_similarity_required matching_quality_threshold
  set minimum_productivity_required firing_quality_threshold
  set exceptional_firing unexpected_firing
  set exceptional_company_motivation unexpected_company_motivation
  set exceptional_worker_motivation unexpected_worker_motivation
  set exceptional_event_proba unexpectedl_event_probability
  
  ; autres variables 
  set timeout 1000
  
  set color-person-employed 67
  set color-person-unemployed 17
  set color-company-filled-job 63
  set color-company-vacant-job 17
  
  set labor_force nb_of_persons ; on considère pour le moment comme agents que les travailleurs dans un système fermé 
  set convergence False
  set last-values-unemployement []
  set last-values-vacancy []
  set nb-of-step-remembered 10 ; on calcule la convergence sur les 10 derniers pas de temps
  set convergence-margin 0.05 ; on fixe la marge à 5% quand les valeurs considérées varient de moins de 5 % sur les 10 derniers pas de temps, on considère que le système a convergé
end




; procédure d'initialisation de l'agent MATCHING
to setup-matching-agent
  set-default-shape matching "square 2"
  create-matching 1 [
    set color white
    set size 2
    setxy 0 0
  ]
end

; procédure d'initialisation des agents PERSON
to setup-persons
  set-default-shape persons "person"
  set-default-shape companies "house"
  create-persons nb_of_persons [
    set color color-person-unemployed
    set size 2
    set-random-skills-location-salary
    set-productivity
    set employed False
    set employer nobody
    set-xy
  ]
end

; procédure d'initialisation des agents COMPANY
to setup-companies
  create-companies nb_of_companies [
    set color color-company-vacant-job
    set size 2
    set-random-skills-location-salary
    set job_filled False
    set employee nobody
    set-xy
  ]
end

; procédure qui fixe au hasard les valeurs de compétences, lieu et salaires pour chaque agent (PERSON et COMPANY)
to set-random-skills-location-salary
  set skills (list random 2 random 2 random 2 random 2 random 2)
  set location random nb_of_locations_possibles
  set salary minimum_salary + random (max_salary - min_salary)
end

; procédure qui fixe la valeur de référence de la productivité de l'employé au hasard selon une loi normale (pour le pas avoir de valeurs trop extrèmes)
to set-productivity
  set reference_productivity random-normal 0.8 0.3
end
